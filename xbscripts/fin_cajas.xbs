/* fin_cajas.xbs
 * Gestión de Cajas
 */

#include "tpy_xbs.ch"

procedure fin_cajas( oForm, oWndParent, oEntParent, cCodCaja, cFiltro, cDocumento )

   local oCajaWnd, oRes, oImgBanner, oImageLogo, oBoxData
   local oLabTitulo, cLabTitulo := "Cajas"
   local rCajas, oModCajas, oLBoxCli, hParam
   local rFinanzas

   default cCodCaja to ""
   default cFiltro     to ""
   default cDocumento  to ""

   if !Empty(cDocumento) .or. !Empty(cFiltro)
      hParam := hb_hash()
   endif

   if !Empty( cDocumento )
      hParam["documento"] := cDocumento
   endif
   if !Empty( cFiltro )
      hParam[ "filtro" ] := cFiltro
   endif

   rFinanzas := ::rEmpresa:SetModulo("Finanzas")
   rCajas  := rFinanzas:Cajas()

   if hb_isNIL(rCajas)
      MsgStop("???")
      return
   endif


   SET RESOURCES oRes FROM FILE oTPuy:cResources+"vta_listbox.ui"


   DEFINE WINDOW oCajaWnd ;
          TITLE "Fiscalius. Caja" ;
          SIZE 700,400 ;
          ID "window1" RESOURCE oRes

          if oWndParent != NIL 
             gtk_window_set_transient_for( oCajaWnd:pWidget, oWndParent:pWidget )
          endif

   DEFINE LABEL oLabTitulo TEXT cLabTitulo ;
          ID "lab_title" RESOURCE oRes

   DEFINE IMAGE oImgBanner ;
          FILE oTPuy:cImages+"banner_02.png"; 
          ID "image1" RESOURCE oRes
          oImgBanner:Adjust(900)

   DEFINE IMAGE oImgLogo ;
          FILE oTPuy:cImages+"fiscalius_logo_02.png";
          ID "image2" RESOURCE oRes
          oImgLogo:Adjust(60)

   DEFINE BOX oBoxData ID "boxdata" RESOURCE oRes


   DEFINE MODEL oModCajas ;
          STRUCT rCajas:Struct() ;
          DATA   rCajas:GetData()
   
   DEFINE LISTBOX oLBoxCli ;
          MODEL oModCajas ;
          OF oBoxData

   oLBoxCli:lBar  := .f.

   // Por defecto, permitir agregar y editar
   oLBoxCli:bNew  := {|oModel| fcaj_edit( oForm, oCajaWnd, oModCajas, rFinanzas, .T. /*lNew*/ ) }
   oLBoxCli:bEdit := {|oModel| fcaj_edit( oForm, oCajaWnd, oModCajas, rFinanzas, .F. /*lNew*/ ) }

   // Si hay oEntParent, cambiar bEdit para seleccionar y cerrar
   if hb_isObject( oEntParent )
      oLBoxCli:bEdit := {|oModel| (cCodCaja := oModel:GetCol(1),; 
                                   iif( !hb_isObject(oWndParent),                ;
                                         .T.,                                     ;
                                         oEntParent:setText( ALLTRIM(cCodCaja)+" - "+;
                                                             oModel:GetCol(3) )), ;
                                   oCajaWnd:End() ) }
   endif
   
   oLBoxCli:SetColVisible( 1, .F. )    //- Ocultamos ID

   oLBoxCli:SetColTitle( 2, "Código" )          //- Cambiamos el titulo 
   oLBoxCli:SetColTitle( 3, "Nombre" )          //- Cambiamos el titulo 
   oLBoxCli:SetColTitle( 4, "Moneda Principal" )//- Cambiamos el titulo 
   oLBoxCli:SetColTitle( 5, "Activa" )          //- Cambiamos el titulo 

   oLBoxCli:SetColVisible(  6, .F. )    //- Ocultamos ID
   oLBoxCli:SetColVisible(  7, .F. )    //- Ocultamos ID
   oLBoxCli:SetColVisible(  8, .F. )    //- Ocultamos ID
   oLBoxCli:SetColVisible(  9, .F. )    //- Ocultamos ID
   oLBoxCli:SetColVisible( 10, .F. )    //- Ocultamos ID
   oLBoxCli:SetColVisible( 11, .F. )    //- Ocultamos ID
   oLBoxCli:SetColVisible( 12, .F. )    //- Ocultamos ID

   ACTIVATE WINDOW oCajaWnd MODAL //OF oWndParent

return


procedure fcaj_edit( oForm, oCajaWnd, oModel, rFinanzas, lNew )
   local oRes, oCajaForm, hData

   default lNew := .T.

   SET PUBLIC oCajaForm

   oCajaForm:lForce   := .F.
   oCajaForm:lClose   := .F.
   oCajaForm:oModel   := oModel
   oCajaForm:rSession := ::rSession
   oCajaForm:rFinanzas  := rFinanzas
   oCajaForm:lNew     := lNew

   oCajaForm:oAliWnd  := oCajaWnd
   oCajaForm:oModel   := oModel
   oCajaForm:oParent  := oForm

   if lNew
      oCajaForm:cCodigo     := ""
      oCajaForm:cNombre     := ""
      oCajaForm:cMoneda     := "VES"
      oCajaForm:lActivo     := .T.
   else
      oCajaForm:cCodigo      := oModel:GetCol( 2 )
      oCajaForm:cPreCodigo   := oModel:GetCol( 2 )
      oCajaForm:cNombre     := oModel:GetCol( 3 )
      oCajaForm:cMoneda     := oModel:GetCol( 4 )
      oCajaForm:lActivo     := oModel:GetCol( 5 )
   endif

   oCajaForm:rCaja := rFinanzas:Caja( oCajaForm:cCodigo )

   hData := oCajaForm:rCaja:Data()
   
   SET RESOURCES oRes FROM FILE oTPuy:cResources+"fin_cajas.ui"

   DEFINE WINDOW oCajaForm:oWnd TITLE "Caja" SIZE 650,350 ;
          OF oCajaWnd ;
          ID "window1" RESOURCE oRes

      DEFINE IMAGE oCajaForm:oImgBanner ID "img_banner" RESOURCE oRes
      oCajaForm:oImgBanner:Adjust(900)

      DEFINE IMAGE oCajaForm:oImgLogo ID "img_logo" RESOURCE oRes
      oCajaForm:oImgLogo:Adjust(60)

      // Código
      DEFINE ENTRY oCajaForm:oEntCodigo VAR oCajaForm:cCodigo ;
             VALID iif( oCajaForm:lClose, .T., __ValCodCaja( oCajaForm:oEntCodigo, oCajaForm ) ) ;
             ID "ent_codigo" RESOURCE oRes

      // Nombre
      DEFINE ENTRY oCajaForm:oEntNombre VAR oCajaForm:cNombre ID "ent_nombre" RESOURCE oRes

      // Moneda Principal (reutilizando campo de comportamiento)
      DEFINE ENTRY oCajaForm:oEntMoneda VAR oCajaForm:cMoneda ID "ent_comporta" RESOURCE oRes

      // Activo (reutilizando checkboxes)
      DEFINE CHECKBOX oCajaForm:oChkActivo VAR oCajaForm:lActivo ID "chk_activo" RESOURCE oRes

      // Ocultar campos no usados
      // No hay forma fácil de ocultarlos en XBScript sin modificarlos

      DEFINE BUTTON oCajaForm:oBtnSave ACTION __FinCajaSave( oCajaForm ) ID "btn_save" RESOURCE oRes

      DEFINE BUTTON oCajaForm:oBtnCancel ACTION ( oCajaForm:lClose := .T., oCajaForm:oWnd:End() ) ;
             ID "btn_cancel" RESOURCE oRes

   ACTIVATE WINDOW oCajaForm:oWnd MODAL VALID __FinCajaExit( oCajaForm )

return


Procedure __FinCajaSave( oForm )
   local hData, uResp, aIter, lNew

   // Validar código antes de guardar
   if Empty( oForm:cCodigo )
      MsgAlert("El código de caja es obligatorio.", "Error")
      oForm:oEntCodigo:SetFocus()
      return
   endif

   // Validar nombre antes de guardar
   if Empty( oForm:cNombre )
      MsgAlert("El nombre de la caja es obligatorio.", "Error")
      oForm:oEntNombre:SetFocus()
      return
   endif

   lNew := oForm:rCaja:IsNew()

   // Guardar los datos de la caja
   oForm:rCaja:SetData( { "codigo"         => oForm:cCodigo,  ;
                          "nombre"         => oForm:cNombre,  ;
                          "moneda_principal" => oForm:cMoneda, ;
                          "activa"        => oForm:lActivo } )

   uResp := oForm:rCaja:Save()

   if !( ValType( uResp ) = "H" .and. hb_hHasKey(uResp,"return") .and. uResp["return"] = .T. )
      if ValType( uResp ) = "H" .and. hb_hHasKey(uResp,"message")
         MsgAlert("No se pudo guardar la caja: " + uResp["message"], "Error")
      else
         MsgAlert("No se pudo guardar la caja.", "Error")
      endif
      return
   endif

   hData := oForm:rCaja:Data()

   // Actualizar el modelo
   if lNew 
      // Caja nueva: añadir una nueva fila al modelo
      aIter := ARRAY(5)
      APPEND LIST_STORE oForm:oModel:oLbx ITER aIter ;
             VALUES hData["id"], ;
                    hData["codigo"], ;
                    hData["nombre"], ;
                    hData["moneda_principal"], ;
                    hData["activa"]
      gtk_tree_view_set_cursor( oForm:oModel:oTreeView:pWidget, oForm:oModel:oTreeView:GetPath( aIter ), NIL, .F. )
   else
      // Caja existente: actualizar la fila seleccionada
      oForm:oModel:SetValue( 2, hData["codigo"] )
      oForm:oModel:SetValue( 3, hData["nombre"] )
      oForm:oModel:SetValue( 4, hData["moneda_principal"] )
      oForm:oModel:SetValue( 5, hData["activa"] )
   endif

   oForm:lForce := .T.
   oForm:oWnd:End()

return


Function __ValCodCaja( oEntCodigo, oForm )
   local cCodigo, rCaja

   cCodigo := oEntCodigo:GetValue()

   // No permitir código vacío
   if Empty( cCodigo )
      MsgAlert( "Debe indicar un código para la caja.", "Atención" )
      return .F.
   endif

   // Verificar longitud 
   if LEN( cCodigo ) < 2 .OR. LEN( cCodigo ) > 10
      MsgAlert( "El código debe tener entre 2 y 10 caracteres.", "Error" )
      oEntCodigo:SetFocus()
      return .F.
   endif

   // Verificar unicidad del código
   rCaja := oForm:rFinanzas:Cajas()
   if rCaja:RecCount() > 0 .and. ( oForm:lNew .or. cCodigo != oForm:cPreCodigo )
      // Buscar si existe el código
      rCaja:GoTop()
      while !rCaja:Eof()
         if ALLTRIM(rCaja:FieldGet(2)) == cCodigo
            MsgAlert( "El código ingresado ya está registrado.", "Atención" )
            return .F.
         endif
         rCaja:Skip()
      end
   endif

   return .T.



Function __FinCajaExit( oForm )

  oForm:lClose := .T. //Activar bandera de cierre antes de validar salida
  if oForm:lForce; return .T. ; endif

  if MsgNoYes("¿Desea salir y descartar los cambios?", "Atención.")
     return .T.
  endif

return .F.

//eof
