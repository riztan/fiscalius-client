/* fin_reibo.xbs
 *
 */

#include "tpy_xbs.ch"
#define SEPARATOR  " "+CHR(199)+CHR(129)+" "


procedure fin_recibo( oFormParent, oFormGParent, cTipoDoc )

   local oForm, oModTmp
   local aMedio, aData := {}

   SET PUBLIC oForm

   ::cEmpresa  := oFormParent:cEmpresa
//   ::rFinanzas := oFormParent:oGEmpresa:SetModulo("Finanzas")

   ::cTipoDoc:= cTipoDoc
   ::cNumDoc := ""
   ::nSaldo  := 0

   ::cCodCli := ""
   ::cEntCli := ""
   ::lParent := .F.

   //-- crear en caso de no venir de un documento
   ::rEmpresa  := NIL
   ::rFinanzas := NIL

   if hb_isObject( oFormParent )
      ::cCodCli := oFormParent:cCodCli
      ::cEntCli := oFormParent:cEntCli
      ::lParent := .T.

      ::rEmpresa  := oFormParent:rEmpresa
      ::rFinanzas := ::rEmpresa:SetModulo("Finanzas")

      ::cTipoDoc  := cTipoDoc
      ::cDocNro   := oFormParent:oLabNumero:GetValue()
      ::cDocMonto := oFormParent:oEntTotal:GetValue()
      ::cDocSaldo := ::cDocMonto
      ::nSaldo    := ToNum( ::cDocSaldo )
   endif

   SET RESOURCES ::oRes FROM FILE oTPuy:cResources+"fin_recibo.ui"

   DEFINE WINDOW ::oWndRecibo TITLE "Cobranza. Recibo " ;
      ID "window1" RESOURCE ::oRes ;
      OF oFormParent:oFacWnd

   DEFINE IMAGE ::oImgBanner ;
          FILE oTPuy:cImages+"banner_02.png" ;
          ID "image1" RESOURCE ::oRes
          ::oImgBanner:Adjust(900)

   DEFINE IMAGE ::oImgLogo ;
          FILE oTPuy:cImages+"fiscalius_logo_02.png";
          ID "image2" RESOURCE ::oRes
          ::oImgLogo:Adjust(60)

   DEFINE LABEL ::oLabInfo    TEXT "" MARKUP ID "lab_info"    RESOURCE ::oRes
//   DEFINE LABEL ::oLabUsuario TEXT "" MARKUP ID "lab_usuario" RESOURCE ::oRes
//   DEFINE LABEL ::oLabEmpresa TEXT "" MARKUP ID "lab_empresa" RESOURCE ::oRes

//::oLabInfo:SetText("Probando Sssonido")

   DEFINE ENTRY  ::oEntCli VAR ::cEntCli ;
       ACTION oTPuy:RunXBS("vta_clientes", oFormParent, ::oWndRecibo, ::oEntCli, @::cCodCli) ;
       VALID  .T. ;
       ID "ent_clientes" RESOURCE ::oRes
   
   if ::lParent
      ::oEntCli:SetEditable(.f.)
      ::oEntCli:SetCanFocus(.F.)

      DEFINE ENTRY ::oEntDocNro VAR ::cDocNro ;
             ID "ent_doc_nro" RESOURCE ::oRes

      DEFINE ENTRY ::oEntDocMonto VAR ::cDocMonto ;
             ID "ent_doc_monto" RESOURCE ::oRes

      DEFINE ENTRY ::oEntDocSaldo VAR ::cDocSaldo ;
             ID "ent_doc_saldo" RESOURCE ::oRes

   endif

   DEFINE FRAME ::oFrameDocsPend ID "frame_docs_pend" RESOURCE ::oRes
   DEFINE BOX ::oBoxDocsPend ID "box_docs" RESOURCE ::oRes 
   
   if !::lParent
      ::oDocuments := ::rFinanzas:CuentasPorCobrar( {"cliente" => "riztan", "incluir_cero" => .t. } )

         if ::lParent //!hb_isNIL( ::rFactDetal )

            DEFINE MODEL   ::oModDocuments       ;
                   STRUCT  ::oDocuments:Struct()  ;
                   DATA    ::oDocuments:GetData()
         else
            DEFINE MODEL   ::oModDocuments           ;
                   STRUCT  {{"empresa_id",     "N", 10, 0},;
                            {"cliente_id",     "N", 10, 0},;
                            {"codigo_cliente" ,"C", 50, 0},;
                            {"cliente",        "C",100, 0},;
                            {"rif",            "C", 20, 0},;
                            {"transaccion_id", "N", 10, 0},;
                            {"tipo_documento", "C", 50, 0},;
                            {"documento",        "C", 15, 0},;
                            {"fecha_documento",  "D", 10, 0},;
                            {"monto_documento",  "N", 10, 2},;
                            {"monto_aplicado", "N", 10, 2},;
                            {"saldo_pendiente","N", 10, 2},;
                            {"estado_cxc",     "C",200, 0}};
                   DATA    {{0,'','','',0,'','',0,0,0,'',''}}

         endif   


         DEFINE LISTBOX ::oLBoxDocsPen     ;
                MODEL   ::oModDocuments    ;
                OF      ::oBoxDocsPend

         ::oLBoxDocsPen:lBar := .F.

         oModTmp := ::oModDocuments 
         ::oLBoxDocsPen:bEdit := {| oModTmp | __DetalDocument( oForm,                                ;
                                                               oModTmp:oTreeView:GetAutoValue(  6 ), ;
                                                               oModTmp:oTreeView:GetAutoValue( 11 ), ;
                                                               ::rFinanzas ) }

         ::oLBoxDocsPen:SetColVisible( 01, .f. )  // empresa_id
         ::oLBoxDocsPen:SetColVisible( 02, .f. )  // cliente_id
         ::oLBoxDocsPen:SetColVisible( 03, .f. )  // codigo_cliente
         ::oLBoxDocsPen:SetColVisible( 04, .f. )  // cliente
         ::oLBoxDocsPen:SetColVisible( 05, .f. )  // rif
         ::oLBoxDocsPen:SetColVisible( 06, .f. )  // transaccion_id

         ::oLBoxDocsPen:SetColTitle( 07, "Tipo"          )
         ::oLBoxDocsPen:SetColTitle( 08, "Nro. Documento")  
         ::oLBoxDocsPen:SetColTitle( 09, "Fecha"         )  
         ::oLBoxDocsPen:SetColTitle( 10, "Monto Doc."    )  
         ::oLBoxDocsPen:SetColTitle( 11, "Monto Apli."   )  
         ::oLBoxDocsPen:SetColTitle( 12, "Saldo"         )  
         ::oLBoxDocsPen:SetColTitle( 13, "Estado"        )


         DEFINE BOX ::oBoxDocDetal ID "box_docs_detal" RESOURCE ::oRes

         DEFINE MODEL   ::oModDocDetals           ;
                STRUCT  {{"cobranza_id",    "N", 10, 0},;
                         {"fecha_cobranza", "D", 10, 0},;
                         {"monto_aplicado" ,"N", 10, 2},;
                         {"medios_pago",    "C",100, 0},;
                         {"observaciones",  "C",100, 0}};
                DATA    {{0,'',0,'',''}}

         DEFINE LISTBOX ::oLBoxDocsDetal   ;
                MODEL   ::oModDocDetals    ;
                OF      ::oBoxDocDetal

         ::oLBoxDocsDetal:lBar  := .F.
         ::oLBoxDocsDetal:bEdit := {|| .T. }

         ::oLBoxDocsDetal:SetColTitle( 01, "Recibo"        )
         ::oLBoxDocsDetal:SetColTitle( 02, "Fecha"         )
         ::oLBoxDocsDetal:SetColTitle( 03, "Aplicado"      )
         ::oLBoxDocsDetal:SetColTitle( 04, "Medio de Pago" )
         ::oLBoxDocsDetal:SetColTitle( 05, "Observaciones" )

         ::oLBoxDocsDetal:Active()
         ::oLBoxDocsPen:Active()
   else

      //View("llenamos valores del documento")

   endif

   DEFINE BOX ::oBoxMedios ID "box_medios" RESOURCE ::oRes

   DEFINE IMAGE ::oImgFondo ID "img_fondo" RESOURCE ::oRes 
   ::oImgFondo:Adjust( 250 )

   ::rMediosPago := ::rFinanzas:MediosPago()

   FOR EACH aMedio IN ::rMediosPago:GetData()
      AADD( aData, { aMedio[2], aMedio[3], "", "", "" } )
   NEXT
   
   DEFINE MODEL  ::oModMedios ;
          STRUCT { {"Codigo",      "C", 04, 00 }, ;
                   {"Medio",       "C", 30, 00 }, ;
                   {"Monto",       "C", 18, 00 }, ;
                   {"Soporte Nro", "C", 10, 00 }, ;
                   {"Origen",      "C", 10, 00 }  ;
                 } ;
          DATA   aData 


   DEFINE LISTBOX ::oLBoxMedios   ;
          MODEL   ::oModMedios    ;
          OF      ::oBoxMedios

   ::oLBoxMedios:lBar := .F.

//   ::oLBoxMedios:SetColVisible( 1 /*Id            */, .F. ) 
//   ::oLBoxMedios:SetColVisible( 2 /*Codigo        */, .F. ) 
//   ::oLBoxMedios:SetColVisible( 4 /*Comportamiento*/, .F. ) 
//   ::oLBoxMedios:SetColVisible( 5 /*GenITF        */, .F. ) 
//   ::oLBoxMedios:SetColVisible( 6 /*Activo        */, .F. ) 
//   ::oLBoxMedios:SetColVisible( 7 /*AfectaBco     */, .F. ) 

   ::oLBoxMedios:Active()

   __HacerModeloEditable( oForm )
   __CalcSaldo( oForm )

   DEFINE STATUSBAR ::oStatusBar ;
          TEXT "" ;  //oTPuy:cSystem_Name + " | Hora: "+oTPuy:cTime ;
          ID "statusbar" RESOURCE ::oRes

   ::oStBar := TStatusBar():New( ::oStatusBar )
   ::oStBar:Add( "empresa",{|| ::cEmpresa + SEPARATOR }  )
   ::oStBar:Add( "username",{|| "Usuario: "+oTPuy:oUser:cNombre + " " }  )
   ::oStBar:Refresh()

   ::bTimer := {|| /*::oStBar:Refresh(),*/ ::oLabInfo:SetText("") }

   DEFINE TIMER ::oTimer ;
          INTERVAL 10000 ;
          ACTION EVAL( ::bTimer )

   //ACTIVATE TIMER ::oTimer

   ACTIVATE WINDOW ::oWndRecibo MODAL
   
   if ::lParent
      //::oBoxDocsPend:Hide(.T.)
      ::oFrameDocsPend:Hide(.T.)
      ::oEntDocNro:SetEditable( .F. )
      ::oEntDocMonto:SetEditable( .F. )
      ::oEntDocSaldo:SetEditable( .F. )
   else
      ::oBoxDocDetal:Hide(.T.)
   endif

return



procedure __DetalDocument( oForm, uTransaccion, cAplicado, rFinanzas )
   local rDetalles, nAplicado, aIter := ARRAY(4)

   nAplicado := VAL(cAplicado)

   if nAplicado == 0
      ::oBoxDocDetal:Hide(.T.)
      return
   endif

   ::oBoxDocDetal:Show(.T.)

   rDetalles := rFinanzas:DetallePagosFactura( uTransaccion )

   if !hb_isObject( ::oModDocDetals )
      rDetalles:End()
      return
   endif

   ::oModDocDetals:oTreeView:ClearModel(.T.)
   While !rDetalles:Eof()
      APPEND LIST_STORE ::oModDocDetals:oLbx ITER aIter          ;
             VALUES hb_ntos(rDetalles:cobranza_id),              ;
                    DToC(rDetalles:fecha_cobranza),              ;
                    TRANSFORM( rDetalles:monto_aplicado, P_92 ), ;
                    ALLTRIM(rDetalles:medios_pago),              ;
                    ALLTRIM(rDetalles:observaciones)   
      rDetalles:Skip()
   EndDo

   rDetalles:End()
return



Procedure __SetMedioMonto( oForm, oModel, aIter, cMonto, lIni )
   local nSaldo, nMonto, nMontoAnterior, nColumna := 3

   default lIni := .F.

   nMontoAnterior := ToNum( oModel:oTreeView:GetAutoValue( nColumna ) )
   nSaldo := ::nSaldo
   nMonto := ToNum( cMonto )

   if lIni .AND. nMontoAnterior > 0
      nMonto := nMontoAnterior
   endif

   if nMontoAnterior > 0
      nSaldo := nSaldo + nMontoAnterior
   endif

   if nSaldo = 0
      oModel:Set( aIter, nColumna, "" )
      return
   endif

   if nMonto < 0 ; nMonto := 0 ; endif

   if nMonto <= nSaldo .AND. nMonto>=0
      if nMonto = 0
         oModel:oLbx:Set( aIter, nColumna, "" ) 
      else
         oModel:oLbx:Set( aIter, nColumna, TRANSFORM( nMonto, P_92 ) ) 
      endif
      ::nSaldo := nSaldo-nMonto
      ::oEntDocSaldo:SetText( TRANSFORM( ::nSaldo, P_92 ) )
   elseif nMonto>nSaldo
      oModel:oLbx:Set( aIter, nColumna, TRANSFORM( nSaldo, P_92 ) ) 
      ::nSaldo := 0
      ::oEntDocSaldo:SetText( TRANSFORM( ::nSaldo, P_92 ) ) 
   endif

return



Procedure __HacerModeloEditable( oForm )
   local oCol
   
//   ::oModMedios:oTreeView:bRow_Activated := NIL

   ::oLBoxMedios:bEdit := {| oModMedios | iif( oModMedios:oTreeView:isGetSelected( oModMedios:aIter ),;
                                               (oModMedios:oTreeView:SetPosCol( oModMedios:aIter, 3, .F. ),;
                                                __SetMedioMonto( oForm, oModMedios, oModMedios:aIter, ::cDocSaldo, .T. ) ; 
                                               ), ;
                                               NIL ;
                                             ) ;
                                           }

   /* Columna Monto */
   oCol := ::oModMedios:aCol[ 3 ] // Monto
   oCol:oRenderer:SetEditable( .T. )

//   oCol:oRenderer:OnEditing_Started( {|oSender,pCell, pEditable, cPath | ;
//                                       iif( ::oModMedios:oTreeView:IsGetSelected( ::oModMedios:aIter ), ;
//                                            ::oModMedios:oLbx:Set( ::oModMedios:aIter, 3, "1234" ),;
//                                            NIL ;
//                                          );
//                                     } )

   oCol:oRenderer:bEdited := {|oSender, path, uVal, aIter|;
                               __EditMedio( oForm, oSender, path, uVal, ::oModMedios:aIter,;
                                            ::oModMedios, ::oModMedios:oTreeView, 3 )  }

   oCol:oRenderer:SetColumn( oCol )

   oCol := ::oModMedios:aCol[ 4 ] // Soporte
   oCol:oRenderer:SetEditable( .T. )

return



Procedure __CalcSaldo( oForm )
   local oCxC

//View(::cTipoDoc)
   oCxC := ::rFinanzas:CuentasPorCobrar( { "cliente"        => "riztan",  ;
                                           "incluir_cero"   => .t.,       ;
                                           "documento_id"   => ::cTipoDoc,;
                                           "nro_documento"  => ::cDocNro  ;
                                         };
                                       )

   if !hb_isObject( oCxC )
      MsgAlert( "Problemas para obtener el saldo del documento." )
      return
   endif

   if oCxC:RecCount()>0
      ::nSaldo := ToNum( ::cDocMonto ) - oCxC:monto_aplicado
      ::oEntDocSaldo:SetText( TRANSFORM( ::nSaldo, P_92 ) )
   endif
   oCxC:End()

return



Procedure __EditMedio( oForm, oSender, path, uVal, aIter, oModel, oTreeView, nCol )

   local nVal := ToNum( uVal )

   uVal := TRANSFORM( nVal, P_92 )

   if nVal = 0 ; uVal := "" ; endif

   if oTreeView:IsGetSelected( aIter )
      if nCol = 3
         __SetMedioMonto( oForm, oModel, aIter, uVal )
      else
         oModel:oLbx:Set( aIter, nCol, uVal )
      endif
   endif

   if nCol = 3
      if !Empty(uVal)
         oTreeView:SetPosCol( aIter, 4, .F. )
      endif
   endif
return

//eof
